/*
 * Copyright 2015 Trello, Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'nu.studer.plugindev' version '2.0'

    // Should come along with plugindev, but plugindev uses an old version
    id 'com.jfrog.bintray' version '1.6'
}

apply plugin: 'groovy'

dependencies {
    implementation gradleApi()
    implementation localGroovy()
}

// This makes it easier to figure out issues with Travis CI builds
test {
    testLogging.showStandardStreams = true
}

apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

// Batik dependencies

dependencies {
    implementation 'org.apache.xmlgraphics:batik-codec:1.9'
    implementation 'org.apache.xmlgraphics:batik-anim:1.9'
    implementation 'org.apache.xmlgraphics:xmlgraphics-commons:2.2'
    implementation 'com.romainpiel.svgtoandroid:svgtoandroid:0.1.0'
    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.12'
}

// Plugin publishing

group = 'com.trello'
version = '1.0.0-SNAPSHOT'

plugindev {
    pluginId 'com.trello.victor'
    pluginName 'victor'
    pluginImplementationClass 'com.trello.victor.VictorPlugin'
    pluginDescription 'Gradle plugin that lets an Android project use SVGs as resources.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'android'
    authorId 'danlew42'
    authorName 'Dan Lew'
    authorEmail 'danlew42@gmail.com'
    projectUrl 'https://github.com/trello/victor'
    projectInceptionYear '2015'
    done() // do not omit this
}

// If you want to upload this, make sure to set BINTRAY_USER and BINTRAY_API_KEY in gradle.properties
if (project.hasProperty('BINTRAY_USER') && project.hasProperty('BINTRAY_API_KEY')) {
    bintray {
        dryRun = true // Turn off when actually deploying
        publish = false // Maybe someday turn this on for auto-publishing

        user = "$BINTRAY_USER"
        key = "$BINTRAY_API_KEY"
        pkg {
            repo = 'gradle-plugins'
            userOrg = 'trello'
        }
    }
}
